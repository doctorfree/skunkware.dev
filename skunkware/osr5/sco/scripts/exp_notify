#!/usr/local/bin/gawk -f
#!/usr/bin/awk -f
# @(#) exp_notify.gawk 2.1 96/06/04
# 92/09/06 john h. dubois iii (john@armory.com)
# 92/12/12 Modified to act according to mail preferences file.
# 92/12/20 Report negative balances as credits.
# 92/12/27 Don't report 0 balance even if min notify is negative
# 93/06/25 Added positive balance total display.
# 93/07/03 Added all command line options.
# 94/07/10 v 2.0: Generalized from rent/house-exp processing program.
# 95/01/02 Added j option/SUBJECT parameter.
# 95/08/26 Use only the basename of the program name for the short name.
# 96/03/01 Prefix name to error messages
# 96/06/04 exec commands.

# todo: record amounts owed, and let users specify that they are only mailed
# when the amount owed changes, and also at some regular interval (e.g.
# monthly).

BEGIN {
    Lib = "/usr/local/lib/balances"
    MapFile = "namemap"
    PrefFile = ".balances"
    Signature = "the balances administrator"

    rcFile = ".balconfig"
    OptList = "eotn:p:l:s:r:j:hx"
    Name = "exp_notify"
    Subject = "Balances"
    Usage = \
"Usage: " Name " [-heot] [-n<mapfile>] [-p<preffile>] [-l<libdir>]\n"\
"       [-s<signature>] [-r<reports>] [-j<subject>] [config file]"

    ARGC = Opts(Name,Usage,OptList,0)
    if (ARGC == -1) {
	print OptErr
	exit(1)
    }
    if (ARGC > 2) {
	print Usage
	print "Use -h for help."
	exit(1)
    }
    if ("h" in Options) {
	printf \
"%s: notify users by mail of the balances they owe.\n"\
"%s\n"\
"%s processes the output of programs that track balances\n"\
"and mails users a report describing what they owe.  Those who do not owe\n"\
"anything are not mailed.\n"\
"The information used to configure %s can be supplied on the command\n"\
"line or in a configuration file.  If no config file is specified on the\n"\
"command line, the default file %s is searched for in the user's home\n"\
"directory.  Values assigned in a configuration file are given in the form\n"\
"varname=value\n"\
"\n"\
"Configuration parameters:\n"\
"\n"\
"The report string is given with -r on the command line or assigned to\n"\
"REPORTS in a config file.  There is no default for it, so one of the two\n"\
"must be given.  It has the form:\n"\
"Program arg ...,Mail Preferences Name,Full Name,Default Threshold:...\n"\
"A report description is given for each report to be run.  Report\n"\
"descriptions are separated from each other by colons.  Each report\n"\
"description has one to four fields, separated from each other by commas.\n"\
"The Program field gives the name of the balance program to execute, and\n"\
"any arguments it needs.  The balance programs must either be in the\n"\
"search path or in the library directory.\n"\
"The Mail Preferences Name field gives a one-word name for the report, for\n"\
"use in a preferences file (described later).  If not given or null, the\n"\
"basename of the first word of the Program field is used.\n"\
"The Full Name field gives a verbose name for the report, to be used in\n"\
"mail sent to users.  If not given or null, the Mail Preferences Name is\n"\
"used.\n"\
"The Default Threshold field gives a default threshold for informing users\n"\
"that they have a balance of this type, in fractional dollars.  If not\n"\
"given or null, 0.01 is used, which will inform users of a positive balance\n"\
"of one cent or more.\n"\
"Example: exp -b,expenses,house expenses,10.00:rent -bn\n"\
"Balances programs should produce output in this form:\n"\
"real-name amount [comment]\n"\
"real-name is used to look up the email address to send the report to.\n"\
"The amount should be in dollars (with optional fractional part) without a\n"\
"leading $.  If there are three or more fields, the extra fields are taken\n"\
"to be a comment and are mailed along with the reports.\n"\
"\n"\
"The mapfile is given with -n on the command line or assigned to MAPFILE in\n"\
"a config file.  It maps names given in the output of the balance programs\n"\
"to email addresses to which balance reports should be sent.  If the email\n"\
"address is a local account name, it is also used to find a preferences\n"\
"file in the user's home directory.  The format of the mapfile is:\n"\
"email-address   name\n"\
"...\n"\
"There must be a name given for every name that will appear in the output\n"\
"of any of the balances programs.  Empty lines and lines that begin with\n"\
"'#' are ignored.  Multiple names can be mapped to a single email address.\n"\
"The default is \"%s\" in the library directory.\n"\
"\n"\
"The preferences file is given with -p on the command line or assigned to\n"\
"PREFFILE in a config file.  It is the name of the file that local users\n"\
"may put in their home directories to control the threshold at which they\n"\
"are informed of balances of various types.  The format is:\n"\
"balance-type threshold\n"\
"...\n"\
"where the thresholds are decimal dollar amounts without a leading '$'.\n"\
"The balance-type is the one-word name for the report as given in a report\n"\
"description.  Unrecognized names are ignored.  The default is \"%s\".\n"\
"\n"\
"The libdir is given with -l on the command line or assigned to LIBDIR in a\n"\
"config file.  It is where the mapfile is searched for if an explicit\n"\
"mapfile name is not given, and where the balances programs are searched\n"\
"for in addition to the execution path.  The default is %s.\n"\
"\n"\
"The signature is given with -s on the command line or assigned to\n"\
"SIGNATURE in a preferences file.  It is the tagline appended to mail sent\n"\
"to users.  The default is \"%s\".\n"\
"\n"\
"The subject line used when the mail is sent given with -j on the command\n"\
"line or assigned to SUBJECT in a preferences file.  The default is \"%s\".\n"\
"\n"\
"Other options (put these var name alone on a line in the config file):\n"\
"-h: Print this help information.\n"\
"-e: Email balance reports to users.  Config file var: MAIL\n"\
"-o: Print a report on all users to the standard output (default).\n"\
"    Both -e and -o may be given.  Config file var: OUTPUT\n"\
"-t: Show exactly what would be mailed to users.  Config file var: TELL\n"\
"\n"\
"Example config file:\n"\
"REPORTS=exp -b,expenses,house expenses,10.00:rent -bn\n"\
"PREFFILE=.house\n"\
"MAPFILE=/usr/local/lib/house/inmates\n"\
"LIBDIR=/usr/local/lib/house\n"\
"SUBJECT=House balances\n"\
"SIGNATURE=the daemon of expensives\n",
    Name,Usage,Name,Name,rcFile,MapFile,PrefFile,Lib,Signature,Subject
	exit(0)
    }
    Debug = "x" in Options
    if (ARGC == 2)
	rcFile = ARGV[1]
    else
	rcFile = "~/" rcFile
    if (InitOpts(rcFile,Options2,OptList,
"MAIL,OUTPUT,TELL,MAPFILE,PREFFILE,LIBDIR,SIGNATURE,REPORTS,SUBJECT",0) == -1)
    {
	print Name ": " OptErr ".  Use -h for help."
	exit 1
    }
    if (ARGC == 2 && !READ_RCFILE) {
	printf Name ": Could not read config file '%s'.\n",rcFile
	exit 1
    }
    # Avoid overwriting options givenon cmd line
    for (e in Options2)
	if (!(e in Options))
	    Options[e] = Options2[e]

    if ("l" in Options)
	Lib = Options["l"]
    if ("n" in Options)
	MapFile = Options["n"]
    else
	MapFile = Lib "/" MapFile
    if ("p" in Options)
	PrefFile = Options["p"]
    if ("s" in Options)
	Signature = Options["s"]
    if ("j" in Options)
	Subject = Options["j"]
    if (!("r" in Options)) {
	print Name ": No report string given."
	exit 1
    }

    if (!(NumBalances = SetupReports(Options["r"])))
	exit 1

    # This file maps real names<->email addresses
    # Each line has two fields, email address and real name
    # Multiple lines may be used to map multiple real names to a single
    # email address, since the balance programs may use different names
    # Name2Addr[real-name] = email address
    while ((ret = (getline < MapFile)) == 1)
	if (NF && $1 !~ "^#") {
	    Name2Addr[$2] = $1
	    if (!($1 in Addr2Name))
		Addr2Name[$1] = $2
	}
    close(MapFile)
    if (ret)
	ErrExit("Error reading user mapping file " MapFile,1)

    if (Debug)
	print "Reading preference files (and processing /etc/passwd!)" \
	> "/dev/stderr"
    # Read users' balance mail preference files
    ReadPrefs(Addr2Name,PrefFile,Thresholds)

    # Run all balance programs
    for (BalNum = 1; BalNum <= NumBalances; BalNum++)
	if (ReadBalances(BalanceProgs[BalNum],Name2Addr,Balances,Comments))
	    ErrExit(\
	    "Error running balance program \"" BalanceProgs[BalNum] "\"")

    Mail = "e" in Options
    Output = "o" in Options
    Tell = "t" in Options
    if (!Mail)
	Output = 1
    for (User in Addr2Name) {
	Balance = ProcUser(User,Output,Mail,Tell,Balances,Comments)
	if (Debug)
	    printf "Debug: balance for %s: %.02f\n",User,Balance \
	    > "/dev/stderr"
	Total += Balance	# Accumulate total balances for all users
	if (Balance > 0)
	    PosTotal += Balance	# Accumulate positive balance for all users
    }

    if (Output) {
	printf "\nTotal of positive balances: %.02f\n",PosTotal
	printf "Total of all balances: %.02f\n",Total
    }
}

# Sets the following global arrays:
#    BalanceTypes[1..n]: Long names of balance types, used in mailed messages.
#    BalanceProgs[1..n]: Balance program names & args to run.
#    Thresholds[1..n]:  Default notification thresholds.
#    PrefTypes[1..n]: Short names of balance types as used in preferences file.
#    PrefType2Num[]: Mapping of balance types given in a preferences
#    	file to expense type number.
# Types contains the report descriptions string.
# Example:
# "exp -b,expenses,house expenses,10.00:rent -bn"
function SetupReports(Types,  
NumReports,RepNum,Command,ShortName,Thresh,NumF,CmdF) {
    if (!(NumReports = split(Types,Reports,":"))) {
	printf "%s: Empty report setup string: %s\n",Name,Types > "/dev/stderr"
	return 0
    }
    for (RepNum = 1; RepNum <= NumReports; RepNum++) {
	NumF = split(Reports[RepNum],Fields,",")
	if (!((1 <= NumF) && (NumF <= 4))) {
	    printf "%s: Wrong number of fields in report setup string: %s\n",
	    Name,Reports[RepNum] > "/dev/stderr"
	    return 0
	}
	BalanceProgs[RepNum] = Fields[1]

	# Get short name (mail preferences name).
	if (NumF >= 2 && Fields[2] != "") {  # If a 2nd field, it's short name
	    if ((ShortName = Fields[2]) ~ "[ \t]") {
		printf \
		"%s: Space or tab in preferences file balance name: %s\n",
		Name,ShortName > "/dev/stderr"
		return 0
	    }
	}
	else {			# If not, inherit from command name
	    split(Fields[1],CmdF)
	    ShortName = CmdF[1]
	    sub(".*/","",ShortName)	# get rid of path
	}
	PrefTypes[RepNum] = ShortName
	PrefType2Num[ShortName] = RepNum

	if (NumF >= 3 && Fields[3] != "")    # If a 3rd field, it's long name
	    BalanceTypes[RepNum] = Fields[3]
	else			# If not, inherit from short name
	    BalanceTypes[RepNum] = ShortName

	if (NumF == 4 && Fields[4] != "") {	# If a 4th field, threshold
	    if ((Thresh = Fields[4]) !~ /^[-+.0-9]+$/) {
		printf "%s: Bad threshold in setup string: %s\n",
		Name,Thresh > "/dev/stderr"
		return 0
	    }
	}
	else	# If not, default is 1 cent
	    Thresh = 0.01
	Thresholds[RepNum] = Thresh + 0
    }
    return NumReports
}

# ProcUser: Process user balances & possibly mail them to user.
# User: User (login name) to process balances for
# Output: Whether to print info for balance administrator to output
# Mail: Whether to send mail.
# Globals used: NumBalances, Signature, Subject, BalanceTypes[], Thresholds[],
#               Addr2Name[], PrefTypes[],
function ProcUser(User,Output,Mail,ShowMessages,Balances,Comments,
BigMsg,NamePrinted,BalNum,Balance,Msg,Cmd,MessageFormat,TotBalance) {

    # Keep track of whether we have yet emitted a name for the user to
    # the stream that will be mailed to the balances administrator
    NamePrinted = 0
    for (BalNum = 1; BalNum <= NumBalances; BalNum++) {
	# If user has a balance of this type...
	if ((BalNum,User) in Balances) {	
	    TotBalance += Balance = Balances[BalNum,User]
	    # Generate a balance line to be mailed to the user,
	    # describing either a debit or credit
	    if (Balance >= 0)
		MessageFormat = "You owe $%.02f for %s"
	    else
		MessageFormat = "You have a credit of $%.02f for %s"
	    Msg = sprintf(MessageFormat,abs(Balance),BalanceTypes[BalNum])
	    if (Comments[BalNum,User] != "")
		Msg = Msg ": " Comments[BalNum,User]
	    # If the balance is positive, print the notification message
	    # for the balance admin too, if wanted
	    if (Balance > 0 && Output) {
		if (!NamePrinted) {
		    printf "** %s (%s)\n",Addr2Name[User],User
		    NamePrinted = 1
		}
		printf "%s\n",Msg
	    }
	    # If user has a nonzero balance and it's larger than the
	    # user's threshold for this balance type, add the notification
	    # message to the message that will be mailed to the user.
	    if (Balance && Balance >= Thresholds[User,BalNum])
		BigMsg = BigMsg Msg "\n"
	    else if (Balance > 0 && Output)
		# If the user has a positive balance & is not going to
		# get a message about it, let the balances admin know
		printf "(min %s balance notification: %0.2f)\n",
		PrefTypes[BalNum],Thresholds[User,BalNum]
	}
    }
    # If the user has a message built up, mail it.
    if (BigMsg != "" && (ShowMessages || Mail)) {
	Cmd = "exec mail -s '" Subject "' " User
	if (TotBalance > 0)
	    BigMsg = BigMsg "\nThis is now due and payable.  Thank you :-)"
	BigMsg = BigMsg "\n\n     -- " Signature
	if (Mail) {
	    print BigMsg | Cmd
	    close(Cmd)
	}
	if (ShowMessages)
	    printf "#######\nMessage to be piped into \"%s\":\n%s\n#######\n",
	    Cmd,BigMsg
    }
    return TotBalance
}

# Globals: PW stuff.
#    Thresholds[User,1..n]:  User notification thresholds.
function ReadPrefs(Addr2Name,PrefFile,Thresholds,
AcctName,PWEnt,MailPrefFile,BalNum) {
    # The mail preferences file includes lines of the form
    # balance-type min-balance-notify
    # Other lines are ignored
    # Thresholds[account-name,balance-num] = min-balance-notify
    for (AcctName in Addr2Name) {
	# Look for a pref file only for local users
	if (AcctName ~ "^[a-zA-Z0-9_]+$")
	    if (!getpwnam(AcctName,PWEnt)) {
		printf "%s: Could not get passwd entry for %s.\n",
		Name,AcctName > "/dev/stderr"
	    }
	    else {
		MailPrefFile = PWEnt[PW_HOME] "/" PrefFile
		ReadPrefFile(MailPrefFile,Thresholds,AcctName,PrefType2Num)
	    }
	for (BalNum in Thresholds)
	    if (!((AcctName,BalNum) in Thresholds))
		Thresholds[AcctName,BalType] = Thresholds[BalType]
    }
}

# The indexes of PrefType2Num are the balance types that may appear in a
# preferences file.  The value of each index is the preference type number
# to use as an index in Arr[].
# If Filename is readable, for each balance type name, 
# Arr[Dim,PrefNum] is set to the value it gives.
# If Dim is null, the value is assigned to just Arr[PrefNum].
# Globals: None.
function ReadPrefFile(Filename,Arr,Dim,PrefType2Num,  ret) {
    if (Dim != "")
	Dim = Dim SUBSEP
    while ((ret = (getline < Filename)) == 1) {
	if ($1 in PrefType2Num) {
	    if ($2 !~ "^-?[0-9]+\\.?[0-9]*$")
		printf "%s: Bad value for balance type \"%s\" in %s.\n",
		Name,$1,Filename > "/dev/stderr"
	    else {
		Arr[Dim PrefType2Num[$1]] = $2
		if (Debug)
		    printf "Reading %s: Setting preference for %s to %s.\n",
		    Filename,$1,$2 > "/dev/stderr"
	    }
	}
    }
    if (ret != 0 && Debug)
	printf \
	"%s: Could not read pref file %s: no such file or not readable.\n",
	Name,Filename > "/dev/stderr"
    close(Filename)
}

# ReadBalances: execute a balances program
# The output should have the form
# RealName Amount Comment
# Name2Addr[] is used to map real names to email addresses
# For each output line, Balances[BalNum,account-name] is set to the output
# and Comments[BalNum,account-name] is set to any comment text
# Globals changed: $*
function ReadBalances(Cmd,Name2Addr,Balances,Comments,
Addr,ret,FullCmd,Err) {
    FullCmd = "PATH=$PATH:" Lib "; " Cmd
    while ((ret = (FullCmd | getline)) == 1) {
	if (!NF)
	    continue
	if (!($1 in Name2Addr)) {
	    printf "%s: Unknown name in output of \"%s\": %s\n",Name,Cmd,$1 \
	    > "/dev/stderr"
	    Err = 1
	}
	else {
	    Addr = Name2Addr[$1]
	    Balances[BalNum,Addr] = $2
	    $1 = $2 = ""
	    if ($0 !~ "^[ \t]*$") {
		Comments[BalNum,Addr] = $0
		# Get rid of leading whitespace left from nulling $1 and $2
		sub("^[ \t]+","",Comments[BalNum,Addr])
	    }
	}
    }
    ret = ret || close(FullCmd) || Err
    return (ret)
}


function ErrExit(Message,ExitValue) {
    printf "%s: %s.\n",Name,Message > "/dev/stderr"
    exit(ExitValue)
}

function sign(value) {
    if (value > 0)
	return 1
    else if (value < 0)
	return -1
    else
	return 0
}

function abs(value) {
    if (value >= 0)
	return value
    else
	return -value
}

### Start of ProcArgs library
# @(#) ProcArgs 1.11 96/12/08
# 92/02/29 john h. dubois iii (john@armory.com)
# 93/07/18 Added "#" arg type
# 93/09/26 Do not count -h against MinArgs
# 94/01/01 Stop scanning at first non-option arg.  Added ">" option type.
#          Removed meaning of "+" or "-" by itself.
# 94/03/08 Added & option and *()< option types.
# 94/04/02 Added NoRCopt to Opts()
# 94/06/11 Mark numeric variables as such.
# 94/07/08 Opts(): Do not require any args if h option is given.
# 95/01/22 Record options given more than once.  Record option num in argv.
# 95/06/08 Added ExclusiveOptions().
# 96/01/20 Let rcfiles be a colon-separated list of filenames.
#          Expand $VARNAME at the start of its filenames.
#          Let varname=0 and -option- turn off an option.
# 96/05/05 Changed meaning of 7th arg to Opts; now can specify exactly how many
#          of the vars should be searched for in the environment.
#          Check for duplicate rcfiles.
# 96/05/13 Return more specific error values.  Note: ProcArgs() and InitOpts()
#          now return various negatives values on error, not just -1, and
#          Opts() may set Err to various positive values, not just 1.
#          Added AllowUnrecOpt.
# 96/05/23 Check type given for & option
# 96/06/15 Re-port to awk
# 96/10/01 Moved file-reading code into ReadConfFile(), so that it can be
#          used by other functions.
# 96/10/15 Added OptChars
# 96/11/01 Added exOpts arg to Opts()
# 96/11/16 Added ; type
# 96/12/08 Added Opt2Set() & Opt2Sets()
# 96/12/27 Added CmdLineOpt()

# optlist is a string which contains all of the possible command line options.
# A character followed by certain characters indicates that the option takes
# an argument, with type as follows:
# :	String argument
# ;	Non-empty string argument
# *	Floating point argument
# (	Non-negative floating point argument
# )	Positive floating point argument
# #	Integer argument
# <	Non-negative integer argument
# >	Positive integer argument
# The only difference the type of argument makes is in the runtime argument
# error checking that is done.

# The & option is a special case used to get numeric options without the
# user having to give an option character.  It is shorthand for [-+.0-9].
# If & is included in optlist and an option string that begins with one of
# these characters is seen, the value given to "&" will include the first
# char of the option.  & must be followed by a type character other than ":"
# or ";".
# Note that if e.g. &> is given, an option of -.5 will produce an error.

# Strings in argv[] which begin with "-" or "+" are taken to be
# strings of options, except that a string which consists solely of "-"
# or "+" is taken to be a non-option string; like other non-option strings,
# it stops the scanning of argv and is left in argv[].
# An argument of "--" or "++" also stops the scanning of argv[] but is removed.
# If an option takes an argument, the argument may either immediately
# follow it or be given separately.
# "-" and "+" options are treated the same.  "+" is allowed because most awks
# take any -options to be arguments to themselves.  gawk 2.15 was enhanced to
# stop scanning when it encounters an unrecognized option, though until 2.15.5
# this feature had a flaw that caused problems in some cases.  See the OptChars
# parameter to explicitly set the option-specifier characters.

# If an option that does not take an argument is given,
# an index with its name is created in Options and its value is set to the
# number of times it occurs in argv[].

# If an option that does take an argument is given, an index with its name is
# created in Options and its value is set to the value of the argument given
# for it, and Options[option-name,"count"] is (initially) set to the 1.
# If an option that takes an argument is given more than once,
# Options[option-name,"count"] is incremented, and the value is assigned to
# the index (option-name,instance) where instance is 2 for the second occurance
# of the option, etc.
# In other words, the first time an option with a value is encountered, the
# value is assigned to an index consisting only of its name; for any further
# occurances of the option, the value index has an extra (count) dimension.

# The sequence number for each option found in argv[] is stored in
# Options[option-name,"num",instance], where instance is 1 for the first
# occurance of the option, etc.  The sequence number starts at 1 and is
# incremented for each option, both those that have a value and those that
# do not.  Options set from a config file have a value of 0 assigned to this.

# Options and their arguments are deleted from argv.
# Note that this means that there may be gaps left in the indices of argv[].
# If compress is nonzero, argv[] is packed by moving its elements so that
# they have contiguous integer indices starting with 0.
# Option processing will stop with the first unrecognized option, just as
# though -- was given except that unlike -- the unrecognized option will not be
# removed from ARGV[].  Normally, an error value is returned in this case.
# If AllowUnrecOpt is true, it is not an error for an unrecognized option to
# be found, so the number of remaining arguments is returned instead.
# If OptChars is not a null string, it is the set of characters that indicate
# that an argument is an option string if the string begins with one of the
# characters.  A string consisting solely of two of the same option-indicator
# characters stops the scanning of argv[].  The default is "-+".
# argv[0] is not examined.
# The number of arguments left in argc is returned.
# If an error occurs, the global string OptErr is set to an error message
# and a negative value is returned.
# Current error values:
# -1: option that required an argument did not get it.
# -2: argument of incorrect type supplied for an option.
# -3: unrecognized (invalid) option.
function ProcArgs(argc,argv,OptList,Options,compress,AllowUnrecOpt,OptChars,
ArgNum,ArgsLeft,Arg,ArgLen,ArgInd,Option,Pos,NumOpt,Value,HadValue,specGiven,
NeedNextOpt,GotValue,OptionNum,Escape,dest,src,count,c,OptTerm,OptCharSet)
{
# ArgNum is the index of the argument being processed.
# ArgsLeft is the number of arguments left in argv.
# Arg is the argument being processed.
# ArgLen is the length of the argument being processed.
# ArgInd is the position of the character in Arg being processed.
# Option is the character in Arg being processed.
# Pos is the position in OptList of the option being processed.
# NumOpt is true if a numeric option may be given.
    ArgsLeft = argc
    NumOpt = index(OptList,"&")
    OptionNum = 0
    if (OptChars == "")
	OptChars = "-+"
    while (OptChars != "") {
	c = substr(OptChars,1,1)
	OptChars = substr(OptChars,2)
	OptCharSet[c]
	OptTerm[c c]
    }
    for (ArgNum = 1; ArgNum < argc; ArgNum++) {
	Arg = argv[ArgNum]
	if (length(Arg) < 2 || !((specGiven = substr(Arg,1,1)) in OptCharSet))
	    break	# Not an option; quit
	if (Arg in OptTerm) {
	    delete argv[ArgNum]
	    ArgsLeft--
	    break
	}
	ArgLen = length(Arg)
	for (ArgInd = 2; ArgInd <= ArgLen; ArgInd++) {
	    Option = substr(Arg,ArgInd,1)
	    if (NumOpt && Option ~ /[-+.0-9]/) {
		# If this option is a numeric option, make its flag be & and
		# its option string flag position be the position of & in
		# the option string.
		Option = "&"
		Pos = NumOpt
		# Prefix Arg with a char so that ArgInd will point to the
		# first char of the numeric option.
		Arg = "&" Arg
		ArgLen++
	    }
	    # Find position of flag in option string, to get its type (if any).
	    # Disallow & as literal flag.
	    else if (!(Pos = index(OptList,Option)) || Option == "&") {
		if (AllowUnrecOpt) {
		    Escape = 1
		    break
		}
		else {
		    OptErr = "Invalid option: " specGiven Option
		    return -3
		}
	    }

	    # Find what the value of the option will be if it takes one.
	    # NeedNextOpt is true if the option specifier is the last char of
	    # this arg, which means that if the option requires a value it is
	    # the next arg.
	    if (NeedNextOpt = (ArgInd >= ArgLen)) { # Value is the next arg
		if (GotValue = ArgNum + 1 < argc)
		    Value = argv[ArgNum+1]
	    }
	    else {	# Value is included with option
		Value = substr(Arg,ArgInd + 1)
		GotValue = 1
	    }

	    if (HadValue = AssignVal(Option,Value,Options,
	    substr(OptList,Pos + 1,1),GotValue,"",++OptionNum,!NeedNextOpt,
	    specGiven)) {
		if (HadValue < 0)	# error occured
		    return HadValue
		if (HadValue == 2)
		    ArgInd++	# Account for the single-char value we used.
		else {
		    if (NeedNextOpt) {	# option took next arg as value
			delete argv[++ArgNum]
			ArgsLeft--
		    }
		    break	# This option has been used up
		}
	    }
	}
	if (Escape)
	    break
	# Do not delete arg until after processing of it, so that if it is not
	# recognized it can be left in ARGV[].
	delete argv[ArgNum]
	ArgsLeft--
    }
    if (compress != 0) {
	dest = 1
	src = argc - ArgsLeft + 1
	for (count = ArgsLeft - 1; count; count--) {
	    ARGV[dest] = ARGV[src]
	    dest++
	    src++
	}
    }
    return ArgsLeft
}

# Assignment to values in Options[] occurs only in this function.
# Option: Option specifier character.
# Value: Value to be assigned to option, if it takes a value.
# Options[]: Options array to return values in.
# ArgType: Argument type specifier character.
# GotValue: Whether any value is available to be assigned to this option.
# Name: Name of option being processed.
# OptionNum: Number of this option (starting with 1) if set in argv[],
#     or 0 if it was given in a config file or in the environment.
# SingleOpt: true if the value (if any) that is available for this option was
#     given as part of the same command line arg as the option.  Used only for
#     options from the command line.
# specGiven is the option specifier character use, if any (e.g. - or +),
# for use in error messages.
# Global variables: OptErr
# Return value: negative value on error, 0 if option did not require an
# argument, 1 if it did & used the whole arg, 2 if it required just one char of
# the arg.
# Current error values:
# -1: Option that required an argument did not get it.
# -2: Value of incorrect type supplied for option.
# -3: Bad type given for option &
function AssignVal(Option,Value,Options,ArgType,GotValue,Name,OptionNum,
SingleOpt,specGiven,  UsedValue,Err,NumTypes) {
    # If option takes a value...    [
    NumTypes = "*()#<>]"
    if (Option == "&" && ArgType !~ "[" NumTypes) {	# ]
	OptErr = "Bad type given for & option"
	return -3
    }

    if (UsedValue = (ArgType ~ "[:;" NumTypes)) {	# ]
	if (!GotValue) {
	    if (Name != "")
		OptErr = "Variable requires a value -- " Name
	    else
		OptErr = "option requires an argument -- " Option
	    return -1
	}
	if ((Err = CheckType(ArgType,Value,Option,Name,specGiven)) != "") {
	    OptErr = Err
	    return -2
	}
	# Mark this as a numeric variable; will be propogated to Options[] val.
	if (ArgType != ":" && ArgType != ";")
	    Value += 0
	if ((Instance = ++Options[Option,"count"]) > 1)
	    Options[Option,Instance] = Value
	else
	    Options[Option] = Value
    }
    # If this is an environ or rcfile assignment & it was given a value...
    else if (!OptionNum && Value != "") {
	UsedValue = 1
	# If the value is "0" or "-" and this is the first instance of it,
	# do not set Options[Option]; this allows an assignment in an rcfile to
	# turn off an option (for the simple "Option in Options" test) in such
	# a way that it cannot be turned on in a later file.
	if (!(Option in Options) && (Value == "0" || Value == "-"))
	    Instance = 1
	else
	    Instance = ++Options[Option]
	# Save the value even though this is a flag
	Options[Option,Instance] = Value
    }
    # If this is a command line flag and has a - following it in the same arg,
    # it is being turned off.
    else if (OptionNum && SingleOpt && substr(Value,1,1) == "-") {
	UsedValue = 2
	if (Option in Options)
	    Instance = ++Options[Option]
	else
	    Instance = 1
	Options[Option,Instance]
    }
    # If this is a flag assignment without a value, increment the count for the
    # flag unless it was turned off.  The indicator for a flag being turned off
    # is that the flag index has not been set in Options[] but it has an
    # instance count.
    else if (Option in Options || !((Option,1) in Options))
	# Increment number of times this flag seen; will inc null value to 1
	Instance = ++Options[Option]
    Options[Option,"num",Instance] = OptionNum
    return UsedValue
}

# Option is the option letter
# Value is the value being assigned
# Name is the var name of the option, if any
# ArgType is one of:
# :	String argument
# ;	Non-null string argument
# *	Floating point argument
# (	Non-negative floating point argument
# )	Positive floating point argument
# #	Integer argument
# <	Non-negative integer argument
# >	Positive integer argument
# specGiven is the option specifier character use, if any (e.g. - or +),
# for use in error messages.
# Returns null on success, err string on error
function CheckType(ArgType,Value,Option,Name,specGiven,  Err,ErrStr) {
    if (ArgType == ":")
	return ""
    if (ArgType == ";") {
	if (Value == "")
	    Err = "must be a non-empty string"
    }
    # A number begins with optional + or -, and is followed by a string of
    # digits or a decimal with digits before it, after it, or both
    else if (Value !~ /^[-+]?([0-9]+|[0-9]*\.[0-9]+|[0-9]+\.)$/)
	Err = "must be a number"
    else if (ArgType ~ "[#<>]" && Value ~ /\./)
	Err = "may not include a fraction"
    else if (ArgType ~ "[()<>]" && Value < 0)
	Err = "may not be negative"
    # (
    else if (ArgType ~ "[)>]" && Value == 0)
	Err = "must be a positive number"
    if (Err != "") {
	ErrStr = "Bad value \"" Value "\".  Value assigned to "
	if (Name != "")
	    return ErrStr "variable " substr(Name,1,1) " " Err
	else {
	    if (Option == "&")
		Option = Value
	    return ErrStr "option " specGiven substr(Option,1,1) " " Err
	}
    }
    else
	return ""
}

# Note: only the above functions are needed by ProcArgs.
# The rest of these functions call ProcArgs() and also do other
# option-processing stuff.

# Opts: Process command line arguments.
# Opts processes command line arguments using ProcArgs()
# and checks for errors.  If an error occurs, a message is printed
# and the program is exited.
#
# Input variables:
# Name is the name of the program, for error messages.
# Usage is a usage message, for error messages.
# OptList the option description string, as used by ProcArgs().
# MinArgs is the minimum number of non-option arguments that this
# program should have, non including ARGV[0] and +h.
# If the program does not require any non-option arguments,
# MinArgs should be omitted or given as 0.
# rcFiles, if given, is a colon-seprated list of filenames to read for
# variable initialization.  If a filename begins with ~/, the ~ is replaced
# by the value of the environment variable HOME.  If a filename begins with
# $, the part from the character after the $ up until (but not including)
# the first character not in [a-zA-Z0-9_] will be searched for in the
# environment; if found its value will be substituted, if not the filename will
# be discarded.
# rcfiles are read in the order given.
# Values given in them will not override values given on the command line,
# and values given in later files will not override those set in earlier
# files, because AssignVal() will store each with a different instance index.
# The first instance of each variable, either on the command line or in an
# rcfile, will be stored with no instance index, and this is the value
# normally used by programs that call this function.
# VarNames is a comma-separated list of variable names to map to options,
# in the same order as the options are given in OptList.
# If EnvSearch is given and nonzero, the first EnvSearch variables will also be
# searched for in the environment.  If set to -1, all values will be searched
# for in the environment.  Values given in the environment will override
# those given in the rcfiles but not those given on the command line.
# NoRCopt, if given, is an additional letter option that if given on the
# command line prevents the rcfiles from being read.
# See ProcArgs() for a description of AllowUnRecOpt and optChars, and
# ExclusiveOptions() for a description of exOpts.
# Special options:
# If x is made an option and is given, some debugging info is output.
# h is assumed to be the help option.

# Global variables:
# The command line arguments are taken from ARGV[].
# The arguments that are option specifiers and values are removed from
# ARGV[], leaving only ARGV[0] and the non-option arguments.
# The number of elements in ARGV[] should be in ARGC.
# After processing, ARGC is set to the number of elements left in ARGV[].
# The option values are put in Options[].
# On error, Err is set to a positive integer value so it can be checked for in
# an END block.
# Return value: The number of elements left in ARGV is returned.
# Must keep OptErr global since it may be set by InitOpts().
function Opts(Name,Usage,OptList,MinArgs,rcFiles,VarNames,EnvSearch,NoRCopt,
AllowUnrecOpt,optChars,exOpts,  ArgsLeft,e) {
    if (MinArgs == "")
	MinArgs = 0
    ArgsLeft = ProcArgs(ARGC,ARGV,OptList NoRCopt,Options,1,AllowUnrecOpt,
    optChars)
    if (ArgsLeft < (MinArgs+1) && !("h" in Options)) {
	if (ArgsLeft >= 0) {
	    OptErr = "Not enough arguments"
	    Err = 4
	}
	else
	    Err = -ArgsLeft
	printf "%s: %s.\nUse -h for help.\n%s\n",
	Name,OptErr,Usage > "/dev/stderr"
	exit 1
    }
    if (rcFiles != "" && (NoRCopt == "" || !(NoRCopt in Options)) &&
    (e = InitOpts(rcFiles,Options,OptList,VarNames,EnvSearch)) < 0)
    {
	print Name ": " OptErr ".\nUse -h for help." > "/dev/stderr"
	Err = -e
	exit 1
    }
    if ((exOpts != "") && ((OptErr = ExclusiveOptions(exOpts,Options)) != ""))
    {
	printf "%s: Error: %s\n",Name,OptErr > "/dev/stderr"
	Err = 1
	exit 1
    }
    return ArgsLeft
}

# ReadConfFile(): Read a file containing var/value assignments, in the form
# <variable-name><assignment-char><value>.
# Whitespace (spaces and tabs) around a variable (leading whitespace on the
# line and whitespace between the variable name and the assignment character) 
# is stripped.  Lines that do not contain an assignment operator or which
# contain a null variable name are ignored, other than possibly being noted in
# the return value.  If more than one assignment is made to a variable, the
# first assignment is used.
# Input variables:
# File is the file to read.
# Comment is the line-comment character.  If it is found as the first non-
#     whitespace character on a line, the line is ignored.
# Assign is the assignment string.  The first instance of Assign on a line
#     separates the variable name from its value.
# If StripWhite is true, whitespace around the value (whitespace between the
#     assignment char and trailing whitespace on the line) is stripped.
# VarPat is a pattern that variable names must match.  
#     Example: "^[a-zA-Z][a-zA-Z0-9]+$"
# If FlagsOK is true, variables are allowed to be "set" by being put alone on
#     a line; no assignment operator is needed.  These variables are set in
#     the output array with a null value.  Lines containing nothing but
#     whitespace are still ignored.
# Output variables:
# Values[] contains the assignments, with the indexes being the variable names
#     and the values being the assigned values.
# Lines[] contains the line number that each variable occured on.  A flag set
#     is record by giving it an index in Lines[] but not in Values[].
# Return value:
# If any errors occur, a string consisting of descriptions of the errors
# separated by newlines is returned.  In no case will the string start with a
# numeric value.  If no errors occur,  the number of lines read is returned.
function ReadConfigFile(Values,Lines,File,Comment,Assign,StripWhite,VarPat,
FlagsOK,
Line,Status,Errs,AssignLen,LineNum,Var,Val) {
    if (Comment != "")
	Comment = "^" Comment
    AssignLen = length(Assign)
    if (VarPat == "")
	VarPat = "."	# null varname not allowed
    while ((Status = (getline Line < File)) == 1) {
	LineNum++
	sub("^[ \t]+","",Line)
	if (Line == "")		# blank line
	    continue
	if (Comment != "" && Line ~ Comment)
	    continue
	if (Pos = index(Line,Assign)) {
	    Var = substr(Line,1,Pos-1)
	    Val = substr(Line,Pos+AssignLen)
	    if (StripWhite) {
		sub("^[ \t]+","",Val)
		sub("[ \t]+$","",Val)
	    }
	}
	else {
	    Var = Line	# If no value, var is entire line
	    Val = ""
	}
	if (!FlagsOK && Val == "") {
	    Errs = Errs \
	    sprintf("\nBad assignment on line %d of file %s: %s",
	    LineNum,File,Line)
	    continue
	}
	sub("[ \t]+$","",Var)
	if (Var !~ VarPat) {
	    Errs = Errs sprintf("\nBad variable name on line %d of file %s: %s",
	    LineNum,File,Var)
	    continue
	}
	if (!(Var in Lines)) {
	    Lines[Var] = LineNum
	    if (Pos)
		Values[Var] = Val
	}
    }
    if (Status)
	Errs = Errs "\nCould not read file " File
    close(File)
    return Errs == "" ? LineNum : substr(Errs,2)	# Skip first newline
}

# Variables:
# Data is stored in Options[].
# rcFiles, OptList, VarNames, and EnvSearch are as as described for Opts().
# Global vars:
# Sets OptErr.  Uses ENVIRON[].
# If anything is read from any of the rcfiles, sets READ_RCFILE to 1.
function InitOpts(rcFiles,Options,OptList,VarNames,EnvSearch,
Line,Var,Pos,Vars,Map,CharOpt,NumVars,TypesInd,Types,Type,Ret,i,rcFile,
fNames,numrcFiles,filesRead,Err,Values,retStr) {
    split("",filesRead,"")	# make awk know this is an array
    NumVars = split(VarNames,Vars,",")
    TypesInd = Ret = 0
    if (EnvSearch == -1)
	EnvSearch = NumVars
    for (i = 1; i <= NumVars; i++) {
	Var = Vars[i]
	CharOpt = substr(OptList,++TypesInd,1)
	if (CharOpt ~ "^[:;*()#<>&]$")
	    CharOpt = substr(OptList,++TypesInd,1)
	Map[Var] = CharOpt
	Types[Var] = Type = substr(OptList,TypesInd+1,1)
	# Do not overwrite entries from environment
	if (i <= EnvSearch && Var in ENVIRON &&
	(Err = AssignVal(CharOpt,ENVIRON[Var],Options,Type,1,Var,0)) < 0)
	    return Err
    }

    numrcFiles = split(rcFiles,fNames,":")
    for (i = 1; i <= numrcFiles; i++) {
	rcFile = fNames[i]
	if (rcFile ~ "^~/")
	    rcFile = ENVIRON["HOME"] substr(rcFile,2)
	else if (rcFile ~ /^\$/) {
	    rcFile = substr(rcFile,2)
	    match(rcFile,"^[a-zA-Z0-9_]*")
	    envvar = substr(rcFile,1,RLENGTH)
	    if (envvar in ENVIRON)
		rcFile = ENVIRON[envvar] substr(rcFile,RLENGTH+1)
	    else
		continue
	}
	if (rcFile in filesRead)
	    continue
	# rcfiles are liable to be given more than once, e.g. UHOME and HOME
	# may be the same
	filesRead[rcFile]
	if ("x" in Options)
	    printf "Reading configuration file %s\n",rcFile > "/dev/stderr"
	retStr = ReadConfigFile(Values,Lines,rcFile,"#","=",0,"",1)
	if (retStr > 0)
	    READ_RCFILE = 1
	else if (ret != "") {
	    OptErr = retStr
	    Ret = -1
	}
	for (Var in Lines)
	    if (Var in Map) {
		if ((Err = AssignVal(Map[Var],
		Var in Values ? Values[Var] : "",Options,Types[Var],
		Var in Values,Var,0)) < 0)
		    return Err
	    }
	    else {
		OptErr = sprintf(\
		"Unknown var \"%s\" assigned to on line %d\nof file %s",Var,
		Lines[Var],rcFile)
		Ret = -1
	    }
    }

    if ("x" in Options)
	for (Var in Map)
	    if (Map[Var] in Options)
		printf "(%s) %s=%s\n",Map[Var],Var,Options[Map[Var]] > \
		"/dev/stderr"
	    else
		printf "(%s) %s not set\n",Map[Var],Var > "/dev/stderr"
    return Ret
}

# OptSets is a semicolon-separated list of sets of option sets.
# Within a list of option sets, the option sets are separated by commas.  For
# each set of sets, if any option in one of the sets is in Options[] AND any
# option in one of the other sets is in Options[], an error string is returned.
# If no conflicts are found, nothing is returned.
# Example: if OptSets = "ab,def,g;i,j", an error will be returned due to
# the exclusions presented by the first set of sets (ab,def,g) if:
# (a or b is in Options[]) AND (d, e, or f is in Options[]) OR
# (a or b is in Options[]) AND (g is in Options) OR
# (d, e, or f is in Options[]) AND (g is in Options)
# An error will be returned due to the exclusions presented by the second set
# of sets (i,j) if: (i is in Options[]) AND (j is in Options[]).
# todo: make options given on command line unset options given in config file
# todo: that they conflict with.
function ExclusiveOptions(OptSets,Options,
Sets,SetSet,NumSets,Pos1,Pos2,Len,s1,s2,c1,c2,ErrStr,L1,L2,SetSets,NumSetSets,
SetNum,OSetNum) {
    NumSetSets = split(OptSets,SetSets,";")
    # For each set of sets...
    for (SetSet = 1; SetSet <= NumSetSets; SetSet++) {
	# NumSets is the number of sets in this set of sets.
	NumSets = split(SetSets[SetSet],Sets,",")
	# For each set in a set of sets except the last...
	for (SetNum = 1; SetNum < NumSets; SetNum++) {
	    s1 = Sets[SetNum]
	    L1 = length(s1)
	    for (Pos1 = 1; Pos1 <= L1; Pos1++)
		# If any of the options in this set was given, check whether
		# any of the options in the other sets was given.  Only check
		# later sets since earlier sets will have already been checked
		# against this set.
		if ((c1 = substr(s1,Pos1,1)) in Options)
		    for (OSetNum = SetNum+1; OSetNum <= NumSets; OSetNum++) {
			s2 = Sets[OSetNum]
			L2 = length(s2)
			for (Pos2 = 1; Pos2 <= L2; Pos2++)
			    if ((c2 = substr(s2,Pos2,1)) in Options)
				ErrStr = ErrStr "\n"\
				sprintf("Cannot give both %s and %s options.",
				c1,c2)
		    }
	}
    }
    if (ErrStr != "")
	return substr(ErrStr,2)
    return ""
}

# The value of each instance of option Opt that occurs in Options[] is made an
# index of Set[].
# The return value is the number of instances of Opt in Options.
function Opt2Set(Options,Opt,Set,  count) {
    if (!(Opt in Options))
	return 0
    Set[Options[Opt]]
    count = Options[Opt,"count"]
    for (; count > 1; count--)
	Set[Options[Opt,count]]
    return count
}

# The value of each instance of option Opt that occurs in Options[] that
# begins with "!" is made an index of nSet[] (with the ! stripped from it).
# Other values are made indexes of Set[].
# The return value is the number of instances of Opt in Options.
function Opt2Sets(Options,Opt,Set,nSet,  count,aSet,ret) {
    ret = Opt2Set(Options,Opt,aSet)
    for (value in aSet)
	if (substr(value,1,1) == "!")
	    nSet[substr(value,2)]
	else
	    Set[value]
    return ret
}

# Returns true if option Opt was given on the command line.
function CmdLineOpt(Options,Opt,  i) {
    for (i = 1; (Opt,"num",i) in Options; i++)
	if (Options[Opt,"num",i] != 0)
	    return 1
    return 0
}
### End of ProcArgs library
### Begin pwent library

# @(#) pwent.awk 1.2 96/06/27
# 92/08/10 john h. dubois III (john@armory.com)
# 93/12/13 fixed to not clobber $*
# 96/01/05 Send error messages to /dev/stderr
# 96/05/24 Let getpwnam() return a specific field if requested.
#          Added PW_REAL and PW_OFFICE.
# 96/06/03 Added Type field to getpwent()
# 96/06/24 Allow a Field to be requested for getpwent() also.
# 96/06/29 Added PW_RECORD, and getpwreal().
#          Changed PWLines to be index by record number instead of name.
# 96/11/17 Added getpwuid()

# Require: ReadShells()

# getpwent, getpwnam: get an entry from the passwd file.
# Each of the following passwd functions returns an array which contains
# a passwd file entry.  The array contains the fields of the entry.
# Global variables:
# The following variables are defined with the values of the indexes of the
# entries: PW_NAME, PW_PASSWORD, PW_UID, PW_GID, PW_GCOS, PW_HOME, PW_SHELL
# PWLines[] contains the lines of the password file, indexed by record number,
# starting with 1.
# _pwNames[] is a mapping of name to passwd record number.
# getpwentNum is the number of the next entry to be returned by getpwent().

# Left FS global because making it local does not work in gawk.
function ReadPasswd(  User,Line,i,Ind,ret,OFS) {
    if (PW_Name)
	return 1
    PW_NAME = 1
    PW_PASSWORD = 2
    PW_UID = 3
    PW_GID = 4
    PW_GCOS = 5
    PW_HOME = 6
    PW_SHELL = 7
    PW_REAL = -1	# for PWGetFields()
    PW_OFFICE = -2
    PW_RECORD = -3

    Ind = getpwentNum = 1
    OFS = FS
    FS = ":"
    while ((ret = (getline Line < "/etc/passwd")) == 1) {
	User = Line
	sub(":.*","",User)
	_pwNames[User] = Ind
	PWLines[Ind++] = Line
    }
    FS = OFS
    close("/etc/passwd")
    if (ret) {
	printf "ReadPasswd(): Could not open /etc/passwd.\n" > "/dev/stderr"
	return 0
    }
    return 1
}

# setpwent resets the passwd file entry pointer used by getpwent
# to the first entry.
function setpwent() {
    getpwentNum = 1
}

# getpwent sets PWEnt to the next entry in the passwd file.
# If Type is set to -1, the entry for the next "real" user is returned (others
# are skipped over), where a real user is a user whose login shell is listed in
# /etc/shells.  This requires the ReadShells() function.  Other values for
# Type are not yet defined and are ignored.
# If the last entry has already been returned, 0 is return if Field is null,
# ":" if not.
# If the entry for the next real user has been requested and /etc/shells
# cannot be read, -1 is returned if Field is null, "\n" if not.
# See PWGetFields() for other return values and the meaning of the Field
# parameter.
function getpwent(PWEnt,Type,Field,  entNum) {
    if (!PW_NAME)
	ReadPasswd()
    if (!(getpwentNum in PWLines))
	return Field ? ":" : 0
    if (Type == -1) {
	if (!_DidReadShells && ReadShells(LoginShells) == -1)
	    return Field ? "\n" : -1
	split(PWLines[getpwentNum++],PWEnt,":")
	while (!(PWEnt[PW_SHELL] in LoginShells)) {
	    if (!(getpwentNum in PWLines))
		return Field ? ":" : 0
	    split(PWLines[getpwentNum++],PWEnt,":")
	}
	return PWGetFields("",PWEnt,Field,getpwentNum - 1)
    }
    else {
	entNum = getpwentNum
	return PWGetFields(PWLines[getpwentNum++],PWEnt,Field,entNum)
    }
}

function MakeInd(  Elem,Ind,Line,uid,home) {
    for (Ind = 1; Ind in PWLines; Ind++) {
	Line = PWLines[Ind]
	split(Line,Elem,":")
	uid = Elem[PW_UID]
	if (!(uid in uidInd))
	    uidInd[uid] = Ind
	home = Elem[PW_HOME]
	if (!(home in HomeInd))
	    HomeInd[home] = Ind
    }
    IndDone = 1
}

# PWGetFields() splits PWLine into PWEnt[], and optionally returns a field
# from it.  If PWLine is null, PWEnt[] is assumed to have already been filled
# in with a password entry.
# If Field is not passed or is null, the return value is 1.
# If Field is non-null, it should a PW_ value.  In this case, the value of the
# requested field is returned.
# entNum is the value that PWEnt[PW_RECORD] should be set to.  It should be
# the index in PWLines[] of the record being processed.
# In addition to the PW_ values used by the rest of the functions in this
# library, this function can be passed PW_REAL and PW_OFFICE.
# PW_REAL will get the part of the GCOS field before the first comma.
# PW_OFFICE will get the part of the GCOS field after the first comma.
# If either of these is requested, both values will also be assigned to their
# indices in PWEnt[], unless there is no comma in the GCOS field, in which case
# PW_OFFICE will not be set.
# NOTE: since the global field names are set in ReadShells(), it must be
# executed before any of the field name can be passed.
function PWGetFields(PWLine,PWEnt,Field,entNum,  gcos,ind) {
    if (PWLine != "")
	split(PWLine,PWEnt,":")
    PWEnt[PW_RECORD] = entNum
    if (!Field)
	return 1
    if (Field < 0) {
	if (ind = index(gcos = PWEnt[PW_GCOS],",")) {
	    PWEnt[PW_OFFICE] = substr(gcos,ind+1)
	    PWEnt[PW_REAL] = substr(gcos,1,ind-1)
	}
	else
	    PWEnt[PW_REAL] = gcos
    }
    return PWEnt[Field]
}

# getpwnam sets PWEnt to the passwd entry for login name Name.
# If Name does not exist in the password file, the return value is ":"
# if Field was passed, 0 if not.
# For other return values and parameter explanation, see PWGetFields()
function getpwnam(Name,PWEnt,Field) {
    if (!PW_NAME)
	ReadPasswd()
    if (Name in _pwNames)
	return PWGetFields(PWLines[_pwNames[Name]],PWEnt,Field,_pwNames[Name])
    else
	return Field ? ":" : 0
}

# getpwhome sets PWEnt to the passwd entry whose home dir is Home.
# See getpwnam() for return values and the meaning of the Field param.
function getpwhome(Home,PWEnt,Field) {
    if (!PW_NAME)
	ReadPasswd()
    if (!IndDone)
	MakeInd()
    if (Home in HomeInd)
	return PWGetFields(PWLines[HomeInd[Home]],PWEnt,Field,HomeInd[Home])
    else
	return Field ? ":" : 0
}

# getpwuid sets PWEnt to the passwd entry whose uid is UID.
# See getpwnam() for return values and the meaning of the Field param.
function getpwuid(UID,PWEnt,Field) {
    if (!PW_NAME)
	ReadPasswd()
    if (!IndDone)
	MakeInd()
    if ((UID + 0) in uidInd)
	return PWGetFields(PWLines[uidInd[UID]],PWEnt,Field,uidInd[UID])
    else
	return Field ? ":" : 0
}

# Make an index by real name.  For each passwd file entry, the real-name
# is lowercased and split into components on non-alphanums.   The passwd entry
# index that the name came from is added to the value of each such component
# in the global _RealInd[].  The indexes stored this way are separated by
# commas.  If the real-name contains no alphanums, its index is stored under
# the null index.
function _makeRealInd(  PWEnt,ret,Elem,nelem,i,Component) {
    setpwent()
    while ((ret = getpwent(PWEnt,"",PW_REAL)) != ":") {
	nelem = split(tolower(ret),Elem,/[^a-z0-9]+/)
	for (i = 1; i <= nelem; i++) {
	    Component = Elem[i]
	    if (Component == "" && nelem > 1)
		continue
	    if (Component in _RealInd)
		_RealInd[Component] = _RealInd[Component] "," PWEnt[PW_RECORD]
	    else
		_RealInd[Component] = PWEnt[PW_RECORD]
	}
    }
    _realIndDone = 1
}

# Make Name into a pattern that will match a name that contains all of the
# same name components (sequences of alphanums) in the same order.  If Name
# contains no name components, a null string is returned.
function MakeNamePat(Name,  Elem,nelem,i,Pat,e) {
    nelem = split(Name,Elem,/[^a-zA-Z0-9]+/)
    for (i = 1; i <= nelem; i++) {
	if ((e = Elem[i]) == "")
	    continue
	if (Pat == "")
	    Pat = "(^|[^a-zA-Z0-9])" e
	else
	    Pat = Pat "[^a-zA-Z0-9](.*[^a-zA-Z0-9])?" e
    }
    if (Pat == "")	# If Name contained no alphanums...
	return ""
    Pat = Pat "([^a-zA-Z0-9]|$)"
    return Pat
}

# getpwgreal sets PWEnt to the first passwd entry whose PW_REAL (see
# PWGetFields()) field matches Real.  Matching occurs if the alphanumeric
# components of Real occur in the same order in the entry.  Non-alphanums are
# ignored.  All of the components in Real must occur in the entry, but not all
# of the components in the entry must occur in Real.
# If the given name does not exist in the password file,
# the return value is ":" if Field was passed, 0 if not.
# If Next is true, getpwreal() sets PWEnt to the next passwd entry whose
# PW_REAL field matches the last previous Real parameter passed.
# In this case,  if the last entry has already been returned,
# the return value is ":" if Field was passed, 0 if not.
# Different IgnoreCase and Full parameters may be given when doing a Next
# search.  Both must always be passed; they do not default to the original
# values when doing a Next search.  The only parameter ignored when doing a
# Next search is Real.
# If IgnoreCase is true, case is ignored when searching.
# If Full is true, a match of the full name is required (including any
# punctuation).
# For successful return values and Field parameter explanation,
# see PWGetFields()
# Globals: For the Next search, between invokations these varies store values:
# _getpwrealInd[]: The set of pw indices that matched the query.
# _getpwrealIndInd: The next index in _getpwrealInd[] to look at.
# _getpwrealReal: The Real value passed with the original query.
# _getpwrealPat: Real converted to a component order search pattern.
function getpwreal(Real,PWEnt,Field,IgnoreCase,Full,Next,  ind,name,Pat) {
    if (!Next) {
	if (!PW_NAME)
	    ReadPasswd()
	if (!_realIndDone)
	    _makeRealInd()
	_getpwrealReal = Real
	_getpwrealPat = MakeNamePat(Real)
	# Get first component from Real
	Real = tolower(Real)
	gsub("^[^a-z0-9]+","",Real)
	gsub("[^a-z0-9].*","",Real)
	if (!(Real in _RealInd))
	    return Field ? ":" : 0
	split(_RealInd[Real],_getpwrealInd,",")
	_getpwrealIndInd = 1
    }
    if (Full)
	Pat = _getpwrealReal
    else
	Pat = _getpwrealPat
    if (IgnoreCase)
	Pat = tolower(Pat)
    while (_getpwrealIndInd in _getpwrealInd) {
	ind = _getpwrealInd[_getpwrealIndInd++]
	name = PWGetFields(PWLines[ind],PWEnt,PW_REAL,ind)
	if (IgnoreCase)
	    name = tolower(name)
	if (Full ? (name == Pat) : (name ~ Pat))
	    return PWGetFields("",PWEnt,Field,ind)
    }
    return Field ? ":" : 0
}

### End pwent library
